SELECT DISTINCT 
  ?item
  ?itemLabel 
  ?linkTo
  ?linkToLabel 
  ?typeOfLink
  ?instanceOf
  ?instanceOfLabel
WHERE {
  VALUES ?perpetrator { SVAR:SUBPERPETRATOR }

  # Ownership and organizational relationships
  {
    ?perpetrator wdt:P127+ ?owner.
    BIND(?owner AS ?item)
    BIND(?perpetrator AS ?linkTo)
    BIND("ownedBy" AS ?typeOfLink)
  }
  UNION {
    ?perpetrator wdt:P1830+ ?ownerOf.
    BIND(?ownerOf AS ?item)
    BIND(?perpetrator AS ?linkTo)
    BIND("ownerOf" AS ?typeOfLink)
  }
  UNION {
    ?perpetrator wdt:P355+ ?childOrg.
    BIND(?childOrg AS ?item)
    BIND(?perpetrator AS ?linkTo)
    BIND("childOrgOf" AS ?typeOfLink)
  }
  UNION {
    ?perpetrator wdt:P749+ ?parentOrg.
    BIND(?parentOrg AS ?item)
    BIND(?perpetrator AS ?linkTo)
    BIND("parentOrg" AS ?typeOfLink)
  }
  UNION {
    ?perpetrator wdt:P361+ ?partof .
    MINUS {
      ?partof wdt:P31 wd:Q223371. # exclude stock market index
    }
    BIND(?partof AS ?item)
    BIND(?perpetrator AS ?linkTo)
    BIND("partOf" AS ?typeOfLink)
  }

  OPTIONAL { ?item wdt:P31 ?instanceOf. }
  OPTIONAL { ?item wdt:P576 ?endDate. }

  FILTER ( !BOUND(?endDate) || ?endDate >= "2023-10-01T00:00:00Z"^^xsd:dateTime )

  SERVICE wikibase:label { bd:serviceParam wikibase:language "[AUTO_LANGUAGE],en,es,fr". }
}
