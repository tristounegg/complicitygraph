SELECT DISTINCT 
  ?item
  ?itemLabel 
  ?linkTo
  ?linkToLabel 
  ?typeOfLink
WHERE {
  wd:Q124086054 p:P8031 ?perpStmt.
  ?perpStmt ps:P8031 ?perpetrator.
  {
  ?perpStmt ps:P8031 ?item.
  BIND("isPerpetrator" AS ?typeOfLink)
  BIND(?item AS ?linkTo)  # You can set it to something else if needed
  }
  union
  # direct link
  {
    ?perpetrator wdt:P169 ?linkTo.
    BIND(?perpetrator AS ?item)
    BIND("hasCEO" AS ?typeOfLink)
  }
  UNION {
    ?perpetrator wdt:P1037 ?linkTo.
    BIND(?perpetrator AS ?item)
    BIND("hasDirector" AS ?typeOfLink)
  }
  UNION {
    ?perpetrator wdt:P127 ?linkTo.
    BIND(?perpetrator AS ?item)
    BIND("ownedBy" AS ?typeOfLink)
  }
  # Owner by and owner ceo
  UNION {
    ?perpetrator wdt:P127 ?owner.
    ?owner wdt:P169 ?linkTo.
    BIND(?owner AS ?item)
    BIND("hasCEO" AS ?typeOfLink)
  }
  UNION {
    ?perpetrator wdt:P127 ?owner.
    ?owner wdt:P1037 ?linkTo.
    BIND(?owner AS ?item)
    BIND("hasDirector" AS ?typeOfLink)
  }
  # Owner of?
  
  # childOrg
  UNION {
  ?perpetrator wdt:P355+ ?childOrg.
  BIND(?childOrg AS ?item)
  BIND(?perpetrator AS ?linkTo)
  BIND("childOrgOf" AS ?typeOfLink)
    }
  
  # Parent org
  UNION {
    ?perpetrator wdt:P749+ ?linkTo.
    BIND(?perpetrator AS ?item)
    BIND("subsidiaryOf" AS ?typeOfLink)
  }
  UNION {
    ?perpetrator wdt:P749+ ?parentOrg.
    ?parentOrg wdt:P169 ?linkTo.
    BIND(?parentOrg AS ?item)
    BIND("hasCEO" AS ?typeOfLink)
  }
  UNION {
    ?perpetrator wdt:P749+ ?parentOrg.
    ?parentOrg wdt:P1037 ?linkTo.
    BIND(?parentOrg AS ?item)
    BIND("hasDirector" AS ?typeOfLink)
  }
  # part of recursive
  UNION {
    ?perpetrator wdt:P361+ ?partof.
    ?partof wdt:P1037 ?linkTo.
    BIND(?parentOrg AS ?item)
    BIND("hasDirector" AS ?typeOfLink)
  }
   UNION {
    ?perpetrator wdt:P361+ ?partof.
    ?partof wdt:P169 ?linkTo.
    BIND(?parentOrg AS ?item)
    BIND("hasCEO" AS ?typeOfLink)
  }
  

  SERVICE wikibase:label { bd:serviceParam wikibase:language "[AUTO_LANGUAGE],en". }
}
LIMIT 1000
