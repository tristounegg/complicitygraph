SELECT DISTINCT 
  ?item
  ?itemLabel 
  ?linkTo
  ?linkToLabel 
  ?typeOfLink
  ?instanceOf
  ?instanceOfLabel
WHERE {
  wd:Q124086054 p:P8031 ?perpStmt.
  ?perpStmt ps:P8031 ?perpetrator.

  {
    ?perpStmt ps:P8031 ?item.
    BIND("isPerpetrator" AS ?typeOfLink)
    BIND(?item AS ?linkTo) 
  }
  # Owner/parent/child orgs.  #  add Owner of?
  UNION {
    ?perpetrator wdt:P127+ ?owner.
    BIND(?owner AS ?item)
    BIND(?perpetrator AS ?linkTo)
    BIND("ownedBy" AS ?typeOfLink)
  }
  UNION {
    ?perpetrator wdt:P1830+ ?ownerOf.
    BIND(?ownerOf AS ?item)
    BIND(?perpetrator AS ?linkTo)
    BIND("ownerOf" AS ?typeOfLink)
  }
  UNION {
    ?perpetrator wdt:P355+ ?childOrg.
    BIND(?childOrg AS ?item)
    BIND(?perpetrator AS ?linkTo)
    BIND("childOrgOf" AS ?typeOfLink)
  }
  UNION {
    ?perpetrator wdt:P749+ ?parentOrg.
    BIND(?parentOrg AS ?item)
    BIND(?perpetrator AS ?linkTo)
    BIND("parentOrg" AS ?typeOfLink)
  }
  UNION {
    ?perpetrator wdt:P361+ ?partof.
    BIND(?partof AS ?item)
    BIND(?perpetrator AS ?linkTo)
    BIND("partOf" AS ?typeOfLink)
  }

  OPTIONAL { ?item wdt:P31 ?instanceOf. }

  SERVICE wikibase:label { bd:serviceParam wikibase:language "[AUTO_LANGUAGE],en". }
}
